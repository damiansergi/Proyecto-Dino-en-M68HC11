	p68h11
	
	public	delayX,n_rand

	org	$3000
*****************************************************************************************************************
* delayX: La subrutina recibe por el registro X la cantidad de llamadas a delay, y no devuelve parametros. La funcion realiza un retardo  	*
* de (cantidad de llamados en X*0,5) segundos. En el wookie aproximadamente, 100 repeticiones son 5 segundos de retardo.		*
*****************************************************************************************************************
delayX	equ	*		
	pshx			;hacemos el backup del registro A, B y X

loopd	jsr	delay		;realiza una llamada a la subrutina delay
	dex			;decremento el contador
	bne	loopd		;si el contador llego a 0 terminar
	
	pulx			;restauramos los backups			
	rts


*****************************************************************
* delay: La subrutina no recibe y devuelve parametros. La funcion realiza un	*
* retardo de 0,5 segundos.						*
*****************************************************************
delay	equ	*
	pshx			;guardo el registro X porque se va a modificar	
	pshy			;idem con el registro Y				
	psha			;idem con el registro A			

	ldx 	#98		;inicializo X1					
	ldy 	#152		;inicializo X2					
	ldaa	#11		;inicializo X3					

loop	dex			;unidad minima de repeticion				
	bne 	loop		;de X1 veces					

	ldx 	#98		;reinicio X1					
	dey			;resto uno a X2					
	bne 	loop		;unidad secundaria de repeticion de X2 veces	

	ldy 	#152		;reinicio X2					
	deca			;resto uno a X3					
	bne 	loop		;unidad terciaria de repeticion de X3 veces	

	pula			;restauro el valor del registro A		
	puly			;idem registro Y				
	pulx			;idem registro X				
	rts	

*************************************************************************
* n_rand: La subrutina recibe no recibe parametros. La funcion lee el timer del HC11 y	*
* genera un numero aleatorio entre 0 y 3 y lo devuelve por el registro A		*		
*************************************************************************
n_rand	equ	*
	ldaa	$100F		;cargamos la parte baja del timer	
	anda	#00000011b	;filtramos solo los ultimos 2 bits del byte
	rts
	end